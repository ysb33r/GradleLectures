
// tag::copyfiles[]
task copyFiles ( type : Copy ) {
    from 'sources', {
        include '*.txt'
        exclude 'b.txt'
    }
    into "${buildDir}/text-files"
}
// end::copyfiles[]

// tag::renamefiles[]
task copyFilesAndRename( type : Copy ) {
    from 'sources', {
        include '*.txt'
    }
    into "${buildDir}/renamed-text-files"
    rename ~/(.+)(\.txt)/, '$1.text'
}
// end::renamefiles[]

// tag::editfiles[]
import org.apache.tools.ant.filters.ReplaceTokens

task copyFilesAndModify( type : Copy ) {
    from 'sources', {
        include '*.txt'
    }
    into "${buildDir}/edited-text-files"

    filter { line ->
        line.replaceAll( ~/foo/,'foo.bar')
    }
    filter ReplaceTokens, tokens : ['DEVOXX': 'Devoxx MA is the place to be' ]
}
// end::editfiles[]

// tag::zipit[]
task zipFiles( type : Zip ) {
    from 'sources', {
        include '*.txt'
        exclude 'b.txt'
    }
    into "a-sub-folder"
    destinationDir buildDir
    archiveName 'devoxxma.zip'
}
// end::zipit[]

// tag::tarit[]
task tarFiles( type : Tar ) {
    from 'sources', {
        include '*.txt'
        exclude 'b.txt'
    }
    into "a-sub-folder"
    destinationDir buildDir
    archiveName 'devoxxma.tar.bz2'
    compression Compression.BZIP2
}
// end::tarit[]

// tag::unzipfiles[]
task copyFilesFromZip ( type : Copy ) {
    from zipTree('sources/example.zip'), {
        include '*.txt'
        exclude 'b.txt'
    }
    into "${buildDir}/unzipped-text-files"
}
// end::unzipfiles[]

// tag::untarfiles[]
task copyFilesFromTar ( type : Copy ) {
    from tarTree(resources.gzip('sources/example.tgz')), {
        include '*.txt'
        exclude 'b.txt'
    }
    into "${buildDir}/untarred-text-files"
}
// end::untarfiles[]

// tag::customCopyFiles[]
task customCopyFiles {
    doLast {
        copy {
            from 'sources', {
                include '*.txt'
                exclude 'b.txt'
            }
            into "${buildDir}/custom-text-files"
        }
    }
}
// end::customCopyFiles[]

task clean {
    delete buildDir
}

// This task is purely here to help with workshop testing
task workshoptest {
    dependsOn copyFiles, customCopyFiles, copyFilesAndRename, copyFilesAndModify
    dependsOn zipFiles, tarFiles, copyFilesFromZip, copyFilesFromTar
}

