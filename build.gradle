buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

  dependencies {
    classpath 'org.ysb33r.gradle:vfs-gradle-plugin:1.0'
    classpath 'commons-httpclient:commons-httpclient:3.1'
  }
}

plugins {
    id 'org.asciidoctor.convert' version '1.5.3'
    id 'org.ysb33r.ivypot' version '0.3.7'
    id 'com.github.jruby-gradle.base' version '1.2.1'
}

apply plugin : 'org.ysb33r.vfs'

version = '1.0-Jbcn2016'

ext {

    downloadDir = "${buildDir}/download"
    revealjsVersion = 'master'
    revealjsTemplateVersion = 'master'
    revealjsDir = new File(downloadDir,'reveal.js')
    revealjsTemplateDir = new File(downloadDir,'revealjs-backends/revealjs')

    repositoryFolder = file('.repository')
    distFolder = file('.repository/dists')

    exampleProjects = [
        'jvm-java',
        'jvm-scala',
        'jvm-groovy',
        'jvm-kotlin',
        'jruby',
        'thewholehog'
    ]
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
}

configurations {
    examples
    zinc
}


dependencies {
    examples 'junit:junit:4.1'
    examples 'org.scala-lang:scala-library:2.11.8'
    examples 'org.scala-lang:scala-compiler:2.11.8'
    examples 'org.codehaus.groovy:groovy-all:2.4.3'
    examples 'org.spockframework:spock-core:1.0-groovy-2.4'
//    examples 'com.github.jruby-gradle:jruby-gradle-plugin:1.2.1'
    examples 'gradle.plugin.com.zoltu:kotlin:1.0.1'
    examples 'org.jetbrains.kotlin:kotlin-stdlib:1.0.1-1'
//    examples 'rubygems:colorize:0.7.7'
    zinc "com.typesafe.zinc:zinc:${org.gradle.language.scala.internal.toolchain.DefaultScalaToolProvider.DEFAULT_ZINC_VERSION}"
}



task download << {
    mkdir downloadDir
    vfs {
        if(!revealjsDir.exists()) {
            cp "zip:https://github.com/hakimel/reveal.js/archive/${revealjsVersion}.zip!reveal.js-${revealjsVersion}",
                revealjsDir, recursive: true, overwrite: true
        }
        if(!revealjsTemplateDir.exists()) {
            cp "zip:https://github.com/ysb33r/asciidoctor-reveal.js/archive/${revealjsTemplateVersion}.zip!asciidoctor-reveal.js-${revealjsTemplateVersion}/templates/slim",
                revealjsTemplateDir, recursive: true, overwrite: true
        }
    }
}

download {
    outputs.dir revealjsDir
    outputs.dir revealjsTemplateDir
}

asciidoctor {
    if (!buildDir.exists()) {
        dependsOn jrubyPrepare
        dependsOn download
    }

    gemPath = jrubyPrepare.outputDir

    sources {
        include 'Gradle45min.adoc'
    }

    resources {
        from(sourceDir) {
            include 'images/**'
            include 'styles/**'
            include 'snippets/**'
        }

        from(downloadDir) {
            include 'reveal.js/**'
            include 'asciidoctor-reveal.js/**'
        }
    }

    attributes 'sourcedir': file('examples'),
        imagesoutdir: 'images',
        'source-highlighter': 'coderay',
        gradlever: gradle.gradleVersion

    backends 'revealjs'

    options template_dirs: [revealjsTemplateDir.parentFile.absolutePath]
}

syncRemoteRepositories {

    inputs.properties 'foo1' : configurations.examples
    inputs.properties 'foo2' : configurations.zinc

    repoRoot repositoryFolder

    // tag::repositories[]
    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    // end::repositories[]

    repositories {
        //        maven {
//            url "${com.github.jrubygradle.JRubyPlugin.RUBYGEMS_RELEASE_URL}"
//        }
//        maven { url 'http://rubygems-proxy.torquebox.org/releases' }
    }

    configurations 'examples','zinc'
//    configurations 'jrubyExec'

    includeBuildScriptDependencies = false

    repoArtifactPattern = '[organisation]/[module]/[revision]/[artifact]-[revision](-[classifier])(.[ext])'
    repoIvyPattern = '[organisation]/[module]/[revision]/ivy-[revision].xml'

}

build.dependsOn asciidoctor

exampleProjects.each { projName ->
    String taskName = "example-${projName}"
    tasks.create( taskName,GradleBuild) {
        dir = file("examples/${projName}")
        buildFile = "${dir}/build.gradle"
        tasks = ['build']
    }
    build.dependsOn taskName
    asciidoctor.mustRunAfter taskName
}

