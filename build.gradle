buildscript {
    repositories {
        jcenter()

        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
        classpath 'org.asciidoctor:asciidoctorj-diagram:1.3.1'
    }
}

repositories {
    jcenter()
}


apply plugin : 'org.asciidoctor.convert'

version '1.0.0-DevoxxMA-2016'

ext {
    exercises = [
        'copy-files', 'jvm-java'
    ]
}

task workshopSolutionTest {
    group 'Workshop Preparation'
    description 'Test all of the exercises'
}
exercises.each { exercise ->
    workshopSolutionTest.dependsOn (tasks.create (exercise, GradleBuild) {
        group 'Workshop Preparation'
        description "Tests required tasks in the '${exercise}' exercise"
        tasks = [ 'workshoptest']
        dir = file("exercises/${exercise}")
        buildFile = file("exercises/${exercise}/solution.gradle")
        startParameter.projectCacheDir = file("${projectDir}/exercises/${exercise}/.gradle")
    })
}

task bootstrap {
    dependsOn workshopSolutionTest, asciidoctor
}

task bootstrapClean {
    dependsOn exercises.collect { exercise ->
        tasks.create ("clean${exercise.capitalize()}", GradleBuild) {
            group 'Workshop Preparation'
            description "Cleans the '${exercise}' exercise"
            tasks = [ 'clean']
            dir = file("exercises/${exercise}")
            buildFile = file("exercises/${exercise}/solution.gradle")
            startParameter.projectCacheDir = file("${projectDir}/exercises/${exercise}/.gradle")
        }
    }
}

asciidoctor {

  requires 'asciidoctor-diagram'

  sources {
      include 'GradleWorkshop.adoc'
      include 'copy.adoc'
      include 'java.adoc'
  }

  resources {
    from(sourceDir) {
      include 'images/**'
      include 'styles/**'
      include 'snippets/**'
      exclude 'images/diag-*.png.cache'
    }

  }

   attributes 'exercises' : file('exercises'),
              imagesoutdir : file('src/docs/asciidoc/images'),
              'source-highlighter' : 'coderay',
              gradlever : gradle.gradleVersion,
              imagesdir : file('src/docs/asciidoc/images')


    backends 'html5'

}


task distZip ( type : Zip ) {
    destinationDir buildDir
    archiveName 'GradleWorkshop.zip'
    from projectDir, {
        include 'src/**'
        include 'exercises/**'
        include '.repository/**'
        include 'dists/**'
        exclude 'exercises/*/build'
        exclude 'exercises/*/.gradle'
    }

    include 'build.gradle'
    include 'gradle'
    include 'gradlew*'
    include 'README.adoc'
}


